import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVer = '2.7.5'
        springCloudVer = '2021.0.4'
        undertow = '2.3.0.Final'
        jackson = '2.14.0-rc2'
        yaml = '1.33'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVer}"
}

group = 'ru.sitronics.tn'
//version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    all {
        exclude module: 'spring-boot-starter-tomcat'
    }
    springBom
    cloudBom
    annotationProcessor.extendsFrom springBom, cloudBom
    implementation.extendsFrom springBom, cloudBom
    compileOnly {
        extendsFrom annotationProcessor
    }
    developmentOnly.extendsFrom springBom, cloudBom
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

jar {
    enabled = false
}

dependencies {
    springBom platform(SpringBootPlugin.BOM_COORDINATES)
    cloudBom platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVer}")
    implementation 'org.springframework.cloud:spring-cloud-config-server'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-undertow' exclude module: 'undertow-websockets-jsr'
    runtimeOnly "io.undertow:undertow-core:${undertow}"
    runtimeOnly "org.yaml:snakeyaml:${yaml}"
    runtimeOnly "com.fasterxml.jackson.core:jackson-databind:${jackson}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task copyConfigs(type: Copy) {
    description 'Copy application\'s configs to resources'
    from(layout.projectDirectory) {
        include 'services-configs/**'
    }
    into layout.buildDirectory.dir('resources/main')
}

tasks.named('test') {
    useJUnitPlatform()
}

processResources.dependsOn copyConfigs
processTestResources.dependsOn copyConfigs
